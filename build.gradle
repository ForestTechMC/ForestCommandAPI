plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

allprojects {
    version = '1.0.2-SNAPSHOT'
    group = 'cz.foresttech'

    repositories {
        mavenCentral()
    }
}

build {
    dependsOn clean
    dependsOn jar
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.3'
        testImplementation 'org.mockito:mockito-core:4.0.0'
    }

    if (!(project.name in ['shared'])) {
        apply plugin: 'com.github.johnrengelman.shadow'

        dependencies {
            implementation project(':shared')
        }

        build {
            dependsOn shadowJar
        }

        shadowJar {
            dependsOn(jar)

            archiveFileName = "${rootProject.name}-${project.name}-${project.version}.jar"
            archiveClassifier.set("")
            destinationDirectory = file("$rootDir/build/libs")

            configurations = [project.configurations.runtimeClasspath]
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version

                if (project.name in ['shared']) {
                    from components.java
                } else {
                    artifact tasks.shadowJar
                }
            }
        }
    }

}